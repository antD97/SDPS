
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'application'
    id 'org.beryx.runtime' version '1.12.1'
    id 'edu.sc.seis.launch4j' version '2.4.9'
}

group 'antD'
version '2.5.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation group: 'org.kohsuke', name: 'github-api', version: '1.129'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

jar {
    manifest {
        attributes 'Main-Class': 'antd.sdps.MainKt'
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

mainClassName = 'antd.sdps.MainKt'

ext.genOutputDir = file("$buildDir/resources")

task createProperties() {
    ext.outputFile = file("$genOutputDir/main/version.txt")
    outputs.file(outputFile)
    doLast {
        outputFile.text = "${project.version}"
    }
}

sourceSets.main.output.dir genOutputDir, builtBy: createProperties

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.base', 'java.desktop', 'java.logging', 'jdk.crypto.ec']
}

launch4j {
    headerType = 'gui'
    mainClassName = 'antd.sdps.MainKt'
    outfile = "$project.name-${project.version}.exe"
    jar = "$buildDir/libs/$project.name-${project.version}.jar"
    bundledJrePath = 'jre'
}

task createBundledExe() {
    dependsOn('runtime', 'createExe')
    doLast {
        def bundledExeDir = "$buildDir/bundledExe/$project.name-$project.version"
        copy {
            from "$buildDir/image"
            into "$bundledExeDir/jre"
        }
        copy {
            from "$buildDir/launch4j/$project.name-${project.version}.exe",
                 "$projectDir/bundle"
            into bundledExeDir
        }
    }
}
